module App

import "./crd/Application.pkl"

name = "app"

local app = this

autoSync = true

destination: Application.Destination = new {}

ignoreDifferences: Listing<Application.IgnoreDifference>? = null

project = "default"

namespace = "default"

source: Application.Source?

sources: Listing<Application.Source>?

application: Application = new {
  metadata {
    name = app.name
    namespace = "argocd"
    annotations {
      ["argocd.argoproj.io/compare-options"] = "ServerSideDiff=true"
    }
  }
  spec {
    syncPolicy {
      when (app.autoSync == true) {
        automated {
          prune = true
          selfHeal = true
        }
      }
      syncOptions {
        when (ignoreDifferences != null) {
          "RespectIgnoreDifferences=true"
        }
        "ApplyOutOfSyncOnly=true"
        "AutoCreateNamespace=true"
        "ServerSideApply=true"
      }
    }
    project = app.project
    destination {
      server = "https://kubernetes.default.svc"
      namespace = app.namespace
    }
    ignoreDifferences = app.ignoreDifferences
    source = app.source
    sources = app.sources
  }
}
