extends "@Resource/Resource.pkl"

import "@k8s/api/apps/v1/Deployment.pkl"
import "@k8s/api/apps/v1/StatefulSet.pkl"
import "@k8s/api/core/v1/PodSpec.pkl"
import "@k8s/api/core/v1/ServiceAccount.pkl"
import "@k8s/api/rbac/v1/PolicyRule.pkl"
import "@k8s/api/rbac/v1/Role.pkl"
import "@k8s/api/rbac/v1/RoleBinding.pkl"
import "@k8s/K8sResource.pkl"
import "@NetPol/NetworkRule.pkl"

hidden name: String

hidden namespace: String = "default"

hidden spec: PodSpec

hidden type: "Deployment" | "StatefulSet" = "Deployment"

hidden useServiceAccount: Boolean = false

hidden forceNetworkPolicy: Boolean = false

local moduleThis = this

local prefixLabel = "app.kubernetes.io/name"

hidden statefulSet: StatefulSet = new StatefulSet {
  metadata {
    name = moduleThis.name
    namespace = moduleThis.namespace
    labels {
      [prefixLabel] = moduleThis.name
    }
  }
  spec {
    selector {
      matchLabels {
        [prefixLabel] = moduleThis.name
      }
    }
    serviceName = moduleThis.name
    template {
      metadata {
        labels {
          [prefixLabel] = moduleThis.name
        }
      }
      spec = (moduleThis.spec) {
        enableServiceLinks = false
        automountServiceAccountToken = useServiceAccount
        when (useServiceAccount) {
          serviceAccountName = moduleThis.name
        }
      }
    }
  }
}

hidden deployment: Deployment = new Deployment {
  metadata {
    name = moduleThis.name
    namespace = moduleThis.namespace
    labels {
      [prefixLabel] = moduleThis.name
    }
  }
  spec {
    selector {
      matchLabels {
        [prefixLabel] = moduleThis.name
      }
    }
    template {
      metadata {
        labels {
          [prefixLabel] = moduleThis.name
        }
      }
      spec = (moduleThis.spec) {
        enableServiceLinks = false
        automountServiceAccountToken = useServiceAccount
        when (useServiceAccount) {
          serviceAccountName = moduleThis.name
        }
      }
    }
  }
}

class RuleDefinition {
  name: String
  namespace: String
  rules: Listing<PolicyRule> = new {}
}

hidden policies: Listing<RuleDefinition> = new {}

hidden netRules: Listing<NetworkRule.NetRule> = new {}

hidden serviceAccount: ServiceAccount = new ServiceAccount {
  metadata {
    name = moduleThis.name
    namespace = moduleThis.namespace
  }
}

hidden networkRules: NetworkRule = new {
  networkPolicy {
    metadata {
      name = moduleThis.name
      namespace = moduleThis.namespace
    }
  }
  targetPod {
    matchLabels {
      [prefixLabel] = moduleThis.name
    }
  }
  netRules = moduleThis.netRules ?? new Listing<NetworkRule.NetRule> {}
}

resources: Listing<K8sResource> = new {
  when (!netRules.isEmpty || forceNetworkPolicy) {
    networkRules.networkPolicy
  }
  when (useServiceAccount) {
    serviceAccount
  }
  when (type == "Deployment") {
    moduleThis.deployment
  }
  when (type == "StatefulSet") {
    moduleThis.statefulSet
  }
  for (roleDefinition in policies) {
    new Role {
      metadata {
        name = roleDefinition.name
        namespace = roleDefinition.namespace
      }
      rules = roleDefinition.rules
    }
    new RoleBinding {
      metadata {
        name = roleDefinition.name
        namespace = roleDefinition.namespace
      }
      subjects {
        new {
          kind = "ServiceAccount"
          name = moduleThis.name
          namespace = moduleThis.namespace
        }
      }
      roleRef {
        kind = "Role"
        name = roleDefinition.name
        apiGroup = "rbac.authorization.k8s.io"
      }
    }
  }
}
